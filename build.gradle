buildscript {
    repositories {
        mavenCentral()
    }
    configurations.maybeCreate('pitest')
    dependencies {
        pitest 'org.pitest:pitest-junit5-plugin:0.12'
    }
}

plugins {
    id 'java-library'

    id "com.github.spotbugs" version "4.6.0"
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'

    id 'info.solidsoft.pitest' version '1.5.2'

    id 'maven-publish'
    id 'signing'
}

wrapper {
    gradleVersion = "6.8.2"
    distributionType = Wrapper.DistributionType.ALL
}

pitest {
    testPlugin = 'junit5'
    threads = 4
}

targetCompatibility = '8'
sourceCompatibility = '8'

group = "de.hhu.stups"
version = "0.1.3-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

def parserVersion
if (isSnapshot) {
    parserVersion = '2.9.28-SNAPSHOT'
} else {
    parserVersion = '2.9.27'
}

dependencies {
    implementation group: "de.hhu.stups", name: "bparser", version: parserVersion

    final junitVersion = "5.7.1"

    testImplementation group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.2.1"
    testImplementation group: "nl.jqno.equalsverifier", name: "equalsverifier", version: "3.5.4"
    testImplementation group: "org.assertj", name: "assertj-core", version: "3.19.0"
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion

    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
}

repositories {
    mavenCentral()
    if (isSnapshot) {
        maven {
            name "sonatype snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

pmd {
    toolVersion = '6.31.0'
    // https://stackoverflow.com/a/53696963/10326268
    ruleSets = []
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd.xml')
    // only apply pmd to src/main
    sourceSets = [sourceSets.main]
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    // https://stackoverflow.com/a/52797107/10326268
    options.addStringOption("sourcepath", "")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Value Translator'
                description = 'Library to translate Classical B values into Java objects.'
                url = 'https://github.com/hhu-stups/value-translator'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/hhu-stups/value-translator/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'bivab'
                        name = 'David Schneider'
                        email = 'david.schneider@hhu.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:hhu-stups/value-translator.git'
                    developerConnection = 'scm:git:ssh://github.com:hhu-stups/value-translator.git'
                    url = 'https://github.com/hhu-stups/value-translator'
                }
            }
        }

    }
    repositories {
        maven {
            final def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            final def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                    username project.ossrhUsername
                    password project.ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
