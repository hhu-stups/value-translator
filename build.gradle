import com.github.spotbugs.snom.SpotBugsTask

import java.util.concurrent.TimeUnit

plugins {
    id 'java-library'

    id "com.github.spotbugs" version "6.1.13"
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'

    id 'info.solidsoft.pitest' version '1.15.0'

    id 'maven-publish'
    id 'signing'

    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = "de.hhu.stups"
version = "0.2.2-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

final snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
final releasesStagingRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"
repositories {
    mavenCentral()
    if (isSnapshot) {
        maven {
            name = "Sonatype snapshots"
            url = snapshotsRepoUrl
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor(0, TimeUnit.SECONDS)
    }
}

dependencies {
    api group: "de.hhu.stups", name: "bparser", version: "2.15.4"

    // SuppressFBWarnings annotations are written into the class files (RetentionPolicy.CLASS).
    // If another project uses a value-translator class containing a SuppressFBWarnings annotation,
    // that other project also needs to have SuppressFBWarnings on its compile classpath,
    // otherwise javac will emit a warning.
    // Thus, spotbugs-annotations needs to be a transitive dependency,
    // not just compileOnly or implementation.
    // We can use compileOnlyApi instead of api,
    // because SuppressFBWarnings is not a runtime annotation.

    // Currently, we only use SuppressFBWarnings in the tests,
    // so we can ignore all of this and just use testCompileOnly.
    // Change back to compileOnlyApi if needed.
    testCompileOnly group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.8.6"

    testImplementation group: "nl.jqno.equalsverifier", name: "equalsverifier", version: "3.19.4"
    testImplementation group: "org.assertj", name: "assertj-core", version: "3.27.6"
    testImplementation(platform(group: "org.junit", name: "junit-bom", version: "5.14.2"))
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter"
    testRuntimeOnly group: "org.junit.platform", name: "junit-platform-launcher"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

javadoc {
    // silence warnings on missing javadoc
    options.addBooleanOption('Xdoclint:all,-missing', true)
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

pitest {
    // The junit5PluginVersion setting internally uses the following Maven coordinates:
    // group: "org.pitest", name: "pitest-junit5-plugin", version: <specified below>
    // https://central.sonatype.com/artifact/org.pitest/pitest-junit5-plugin
    junit5PluginVersion = "1.2.3"
    threads = 4
}

pmd {
    toolVersion = "7.20.0"
    // https://stackoverflow.com/a/53696963/10326268
    ruleSets = []
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd.xml')
    // only apply pmd to src/main
    sourceSets = [sourceSets.main]
}

spotbugs {
    toolVersion = '4.8.6'
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Value Translator'
                description = 'Library to translate Classical B values into Java objects.'
                url = 'https://github.com/hhu-stups/value-translator'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/hhu-stups/value-translator/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'bivab'
                        name = 'David Schneider'
                        email = 'david.schneider@hhu.de'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:hhu-stups/value-translator.git'
                    developerConnection = 'scm:git:ssh://github.com:hhu-stups/value-translator.git'
                    url = 'https://github.com/hhu-stups/value-translator'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        mavenCentral {
            snapshotRepositoryUrl.set(uri(snapshotsRepoUrl))
            nexusUrl.set(uri(releasesStagingRepoUrl))
        }
    }
}

ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

signing {
    sign publishing.publications.mavenJava
}
