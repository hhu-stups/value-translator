plugins {
    id 'java-library'

    id "com.github.spotbugs" version "4.7.5"
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'

    id 'info.solidsoft.pitest' version '1.6.0'

    id 'maven-publish'
    id 'signing'
}

wrapper {
    gradleVersion = "7.2"
}

group = "de.hhu.stups"
version = "0.1.3-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
    if (isSnapshot) {
        maven {
            name "sonatype snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

def parserVersion
if (isSnapshot) {
    parserVersion = '2.9.29-SNAPSHOT'
} else {
    parserVersion = '2.9.28'
}

dependencies {
    implementation group: "de.hhu.stups", name: "bparser", version: parserVersion

    testImplementation group: "com.github.spotbugs", name: "spotbugs-annotations", version: "4.4.0"
    testImplementation group: "nl.jqno.equalsverifier", name: "equalsverifier", version: "3.7.1"
    testImplementation group: "org.assertj", name: "assertj-core", version: "3.20.2"
    testImplementation(platform(group: "org.junit", name: "junit-bom", version: "5.7.2"))
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api"

    testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    // https://stackoverflow.com/a/52797107/10326268
    options.addStringOption("sourcepath", "")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

pitest {
    junit5PluginVersion = "0.14"
    threads = 4
}

pmd {
    toolVersion = '6.38.0'
    // https://stackoverflow.com/a/53696963/10326268
    ruleSets = []
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd.xml')
    // only apply pmd to src/main
    sourceSets = [sourceSets.main]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Value Translator'
                description = 'Library to translate Classical B values into Java objects.'
                url = 'https://github.com/hhu-stups/value-translator'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/hhu-stups/value-translator/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'bivab'
                        name = 'David Schneider'
                        email = 'david.schneider@hhu.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:hhu-stups/value-translator.git'
                    developerConnection = 'scm:git:ssh://github.com:hhu-stups/value-translator.git'
                    url = 'https://github.com/hhu-stups/value-translator'
                }
            }
        }

    }
    repositories {
        maven {
            final def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            final def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                    username project.ossrhUsername
                    password project.ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
